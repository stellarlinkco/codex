load("//:defs.bzl", "codex_rust_crate")

codex_rust_crate(
    name = "core",
    crate_name = "codex_core",
    # askama derives `include_str!(concat!(env!("CARGO_MANIFEST_DIR"), ...))`.
    # rules_rust applies path remapping for reproducible builds which can turn
    # absolute paths under the workspace into relative paths (dropping the
    # leading '/'), breaking askama's template discovery. Point at an absolute
    # path that exists inside the sandbox.
    rustc_env = select({
        "@platforms//os:linux": {
            "CARGO_MANIFEST_DIR": "/proc/self/cwd/codex-rs/core",
        },
        "//conditions:default": {},
    }),
    compile_data = glob(
        include = ["**"],
        exclude = [
            "**/* *",
            "BUILD.bazel",
            "Cargo.toml",
        ],
        allow_empty = True,
    ) + [
        "//codex-rs:node-version.txt",
    ],
    integration_compile_data_extra = [
        "//codex-rs/apply-patch:apply_patch_tool_instructions.md",
        "models.json",
        "prompt.md",
    ],
    test_data_extra = [
        "config.schema.json",
        # This is a bit of a hack, but empirically, some of our integration tests
        # are relying on the presence of this file as a repo root marker. When
        # running tests locally, this "just works," but in remote execution,
        # the working directory is different and so the file is not found unless it
        # is explicitly added as test data.
        #
        # TODO(aibrahim): Update the tests so that `just bazel-remote-test`
        # succeeds without this workaround.
        "//:AGENTS.md",
    ],
    integration_deps_extra = ["//codex-rs/core/tests/common:common"],
    test_tags = ["no-sandbox"],
    extra_binaries = [
        "//codex-rs/linux-sandbox:codex-linux-sandbox",
        "//codex-rs/rmcp-client:test_stdio_server",
        "//codex-rs/rmcp-client:test_streamable_http_server",
        "//codex-rs/cli:codex",
    ],
)
